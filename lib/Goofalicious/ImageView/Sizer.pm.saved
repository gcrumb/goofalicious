package Goofalicious::ImageView::Sizer;

use strict;
use warnings;
use File::Basename;
require Image::Magick::Thumbnail;

use vars qw($VERSION);
$VERSION = "0.01";

=pod

=head1 NAME

Goofalicious::ImageView::Sizer - Image Manipulator

=head1 SYNOPSIS

  use Goofalicious::ImageView::Sizer;

  my %args = (
	      file            => "/path/to/file/filename.ext",
	      max_dimension   => 600,
	      suffix          => "size",
	     );
  my $sizer = Goofalicious::ImageView::Sizer->new(%args);

  unless ($sizer->{file_exists}){
    warn "Couldn't create new image\n" unless
      $sizer->create();
  }

=head1 DESCRIPTION

The B<Goofalicious::ImageView::Sizer> module creates new images

It inherits most of its behaviour from L<Image::Magick::Thumbnail>,
a very simple interface to the L<Image::Magick> size manipulation
functions.

=begin testing


=end testing

=head1 Methods

=over 4

=item new

Object constructor. 

B<NOTE:> Best to leave this alone. Subclass the B<init> method
if you want to do customisations.

=begin testing

diag("");
diag("No more tests apply for this package");
diag("");

=end testing

=cut

sub new {

  my ($class, %args) = @_;
  my $self = {};

  bless $self, $class;

  return $self->init(%args);

}

sub init {

  my ($self, %args) = @_;

  return $self unless keys %args;

  my @required = qw( file max_dimension suffix );

  while (my ($key, $value) = each %args){
    $self->{$key} = $value;
  }

  foreach (@required){
    unless (grep($_, keys %args)){
      warn "Uh-oh missing $_ property\n";
    }
  }

  return undef unless -f $self->{file};

  my ($file_name, $path, $suffix) = fileparse($self->{file}, qr{\.jpg});

  if ($self->{suffix} eq "size"){
    $self->{img_name} = $file_name . "_" . $self->{max_dimension}  . $suffix;
  }
  else {
    $self->{img_name} = $file_name . "_" . $self->{suffix} . $suffix;
  }

  $self->{img_path} = $path;

  my $file_to_test = $self->{img_path} . "/" . $self->{img_name};

  if (-f $file_to_test){
    $self->{file_exists} = $self->{img_name};
  }
  else {
    $self->{file_exists} = "";
  }

  return $self;

}

sub create {

  my $self = shift;

  my $new_img_name = $self->{img_path} . "/" . $self->{img_name};
  my $sizer = new Image::Magick;
  $sizer->Read($self->{file});

  my ($thumb,$x,$y) = Image::Magick::Thumbnail::create($sizer, $self->{max_dimension});

  $thumb->Write($new_img_name);

  return $self->{img_name};

}

1;

__END__

=head1 TODO

=over 4

=item *

Nothing yet. 8^)

=back

=head1 SEE ALSO

=over 4

=item *

L<Image::Magick>

=item *

L<Image::Magick::Thumbnail>

=back

=head1 Author

Dan McGarry L<E<lt>dmcgarry@moodindigo.caE<gt>>

=cut
